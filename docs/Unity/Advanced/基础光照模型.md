# 基础光照模型

渲染图像最终的目的就是计算出每个像素的颜色并将之显示在屏幕上。从宏观上来说，像素的颜色一般由可见性以及光照计算来决定，而光照模型则是用来进行光照计算的。

## 光照原理

---

各位想必都学习过物理，那么关于光的有关性质应该是不会陌生的。我们之所以能够看到物体，是因为物体会发射或者反射光线，由我们人眼接收到并形成图像信号。当人们描述某个物体是蓝色时，是因为该物体吸收了其它的波长，并将大多数的蓝色波长反射了出去。为了在游戏中模拟这种真是的物理现象，我们需要对物理的本质有所了解。

通常而言，光照模拟需要考虑三种情况：

* 光线由光源发出。
* 光线被物体吸收或者散射到其他的方向。
* 摄像机吸收了光线，产生图像。

### 光源

为了将光源量化为一个可以描述的物理量，我们可以用**辐射度**来描述光照的强度，用 l 代表光照方向，那么辐射度就表示为在垂直于 l 的单位面积上单位时间内穿过的能量。当然，光照方向一般都是不垂直于物体表面的，所以我们需要进行一些计算。

假设光线之间的距离为 $d$，光线与物体表面的夹角 $\theta$，那么右图中光线在物体表面上的距离为 $d/\cos\theta$，其辐射度显然要比左图小。

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/irradiance.png)

### 吸收和散射

光线与物体相交时通常会有两种情况：**散射**和**吸收**。

散射只改变光线的方向，而吸收则只改变光线的密度和颜色。光线在物体表面进行散射后，通常会有有两种方向：一种会散射到物体内部，即**折射**；另一种会散射到物体外部，即**反射**。对于不透明物体，光线经过折射后还会继续在物体内部碰撞，有可能会被物体吸收或者重新发射出物体表面。下图描述的就是不透明物体的情况。

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/scattering.png)

为了进行区分，我们把物体表面的光线反射称为**高光反射**，把光线折射、吸收、从内部散射出物体表面称为**漫反射**。根据入射光线的数量和方向，可以计算出射光线的数量和方向，一般用**出射度**来描述。辐射度和出射度呈线性关系，它们的比值就是材质的漫反射和高光反射属性。

### 着色

**着色**是指根据材质属性、光源信息，使用一个公式来计算沿某个观察方向的出射度的过程。这个公式称之为**光照模型**。光照模型具有特定的含义，例如有些用于描述粗糙的物体表面，而有些则用于描述金属表面。

### BRDF光照模型

双向反射分布函数（Bidirectional Reflectance Distribution Function，BRDF）用来描述模型上某个顶点的外观，它大多是以数学公式的形式进行表示。当给定入射光线的方向和辐射度之后，BRDF 可以给出在某个出射方向上光照的能量分布。简单点来说，我们可以使用这个模型来求出反射光的数量、方向、强度等等。

本篇笔记所记载的只是最基础的光照模型，简单地计算了入射和反射光线，并且所有的参数都是理想化以及简化的，并不能真实地描述光线和物体的交互。当然，光是这些经验之谈就已经能够满足许多需求了，毕竟只要看起来是正确的就行了。

?> 本章只讨论基础的光照模型，基于物理的 BRDF 会放到之后的笔记中。

## 标准光照模型

---

所谓的标准光照模型，就是重点关注那些直接从光源发射出来照射到物体表面后，经过物体一次反射就直接进入摄像机的光线。

标准光照模型将进入摄像机的光线分为四个部分，它们分别是：

* 自发光：当给定一个方向时，一个表面本身会向该方向发射多少辐射量。
* 高光反射：当光线从光源照射到模型表面时，该表面会在完全镜面反射方向散射多少辐射量。
* 漫反射：当光线从光源照射到模型表面时，该表面会向每个方向散射多少辐射量。
* 环境光：用于描述其他的间接光。

### 环境光

在真实世界中，物体经常会被各种间接光照亮，所以就算标准光照模型的重点在于直接光照，也不能够将环境光全部忽视掉。所谓的间接光照，通常是指光线会在多个物体表面反射，并最终进入到摄像机中。举个例子，如果一个红色的物体反射了光线，那么被间接光所照射到的物体也会带上一点红色。

在标准光照模型中，环境光被用来模拟真是的间接光照。它通常是一个全局变量，所有的物体都会使用这个环境光，因此效果也是比较简单。

### 自发光

直接由光源进入摄像机的光线可以被称为自发光。由于自发光没有经过反射，所以它可以直接使用该材质的自发光颜色。

在 Unity 中，自发光表面其实并不能够照亮周围的表面，因为它并没有被当成一个光源。只有当你开启了全局光照之后，才能够模拟自发光物体对周围物体表面的影响。

### 漫反射

漫反射用于描述物体表面随机散射到各个方向的辐射度。由于反射是随机的，所以可以认为任何反射方向上的分布都是一样的。

漫反射遵循**兰伯特定律**，即反射光线强度与表面法线和光源方向之间的夹角的余弦值成正比。设 $n$ 为表面法线，$I$ 为指向光源的单位矢量，$m_{diffuse}$ 是材质的漫反射颜色，$c_{light}$ 是光源颜色，则有以下计算公式：

$$ c_{diffuse}=(c_{light} \cdot m_{diffuse})max(0, n \cdot I) $$

为了避免法线和光源方向点乘的结果为负值，这里要用 `max` 函数进行取值判断，避免物体被从后面来的光源照亮。

### 高光反射

这里的高光反射其实是一种经验模型，与真实情况的高光反射并不一样。它一般用来计算完全镜面反射的光线，比如金属的表面通常会反射强烈的光线，形成一个亮度很高的亮点。

计算高光反射需要知道表面法线、视角方向、光源方向、反射方向等，不过由于反射方向可以经过计算得到，因此我们一般只需要知道三个矢量即可。

假设 $m_{gloss}$ 是材质的光泽度，它用于控制反射形成的亮点大小，光泽度越高亮点越小。$m_{spscular}$ 是材质的高光反射颜色，用于控制该材质对高光反射的强度和颜色。$c_{light}$ 是光源的颜色和强度。那么我们有如下计算公式：

$$ c_{spscular}=(c_{light} \cdot m_{spscular})max(0, \boldsymbol v \cdot \boldsymbol r)^{m_{gloss}} $$

下图则是该公式对应的模型示意图（Phong 模型）：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/specular.png)

当然，我们也可以不计算反射方向，而是引入一个新的矢量 $\boldsymbol h$，它是由矢量 $\boldsymbol v$ 和矢量 $\boldsymbol I$ 的取平均再归一化得到的。即

$$ \boldsymbol h=\frac{\boldsymbol v + I}{|\boldsymbol v + I|} $$

然后我们就能够得到如下计算公式：

$$ c_{spscular}=(c_{light} \cdot m_{spscular})max(0, \boldsymbol n \cdot \boldsymbol h)^{m_{gloss}} $$

下图则是该公式对应的模型示意图（Blinn 模型）：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/Blinn.png)

一般来说，当摄像机和光源距离模型足够远时，Blinn 模型要更快一些，因为此时可以认为 $\boldsymbol v$ 和 $\boldsymbol r$ 都是定值，也就是说 $\boldsymbol h$ 是常量。

### 逐顶点与逐像素

上面介绍了标准光照模型使用的数学公式，那么我们应该在哪个地方计算光照模型呢？一般来说，我们可以在顶点着色器或者片元着色器中进行计算，顶点着色器的计算被称为**逐顶点光照**，而片元着色器的计算则被称为**逐像素光照**。

片元着色器需要以每个像素为基础进行插值操作，通过对顶点法线进行插值或者从法线纹理中采样从而得到像素对应的法线。这种在片元之间对顶点法线进行插值的技术叫做**Phong着色**，它与之前提到的 Phong 光照模型是不一样的。

对于顶点着色器而言，我们需要在每个顶点上计算光照，然后在渲染图元内部进行线性插值，最后输出像素颜色。由于顶点个数远小于像素个数，因此逐顶点光照要比逐像素光照快得多。不过问题是，由于顶点光照只能用线性插值得到每个像素的颜色，因此当光照模型中含有非线性计算时（比如高光反射），逐顶点光照就会产生问题。另外，由于逐顶点光照是在渲染图元内部对顶点颜色插值，所以会导致渲染图元内部的颜色总是比最高处的顶点要暗。

?> 标准光照模型只是一个经验模型，无法模拟真实的光照。但由于其易用性以及计算速度快，所以使用的范围也很广。当然，如果是要模拟复杂的物理现象，那么就需要使用基于物理的光照模型。

## Shader实现

---

接下来就是对于漫反射以及高光反射的实现，每种反射我都会以逐顶点和逐像素的方式分别实现一遍。

在开始之前，我们可以先设置一下 Unity 场景的环境光。在 `Window -> Rendering -> Lighting Settings` 中可以进行环境光设置，`Source` 选项则是设置光源，比如天空盒、渐变色、单种颜色。为了让 Shader 的效果更明显，可以把天空盒的材质设置为 `None`。

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/01.png)

由于大部分物体没有自发光特性，因此本篇笔记暂时不对自发光进行计算，之后遇到复杂情况时再进行讨论。

### 漫反射

漫反射使用的是兰伯特定律，我们一共需要知道四个参数：入射光线颜色和强度、材质漫反射系数、表面法线、光源方向。另外，为了防止点积结果为负值，我们需要使用 `max` 或者 `saturate` 函数，它们是 CG 代码提供的 API。

逐顶点漫反射的代码如下：

```
Shader "Custom/Level2/DiffuseVertexShader"
{
	Properties
	{
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)      // 材质的漫反射颜色
	}
	SubShader
	{
		Tags { "RenderType"="Opaque" }
		LOD 100

		Pass
		{
			Tags { "LightMode"="ForwardBase" }

			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"
			#include "Lighting.cginc"

			fixed4 _Diffuse;
			
			struct v2f
			{
				float4 pos : SV_POSITION;
				fixed3 color : Color;
			};
			
			v2f vert (appdata_base v)
			{
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				// 环境光
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				// 将法线变换至世界坐标
				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
				// 将光照方向变换至世界坐标
				fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);

				// 反射光颜色
				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(worldNormal, worldLightDir));

				o.color = diffuse + ambient;
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				return fixed4(i.color, 1.0);
			}
			ENDCG
		}
	}

	Fallback "Diffuse"
}
```

代码的逻辑很容易理解，简单来说就是在顶点着色器中按照兰伯特定律计算出反射光颜色即可。这里需要注意的就是如何获取环境光、入射光线颜色等参数。

首先来看环境光。环境光可以使用 `UNITY_LIGHTMODEL_AMBIENT.xyz` 来获取，在计算时只需要直接与反射光颜色相加即可。如果之后还需要加入自发光，那么同样也是将结果与自发光颜色相加。

在矩阵变换一章中，我介绍了法线变换，它需要使用顶点变换矩阵的逆转置矩阵进行变换。Unity 为我们提供了 `UnityObjectToWorldNormal` 函数进行法线变换，它的源代码可以在 `UnityCG.cginc` 中查看：

```
inline float3 UnityObjectToWorldNormal( in float3 norm )
{
#ifdef UNITY_ASSUME_UNIFORM_SCALING
    return UnityObjectToWorldDir(norm);
#else
    // mul(IT_M, norm) => mul(norm, I_M) => {dot(norm, I_M.col0), dot(norm, I_M.col1), dot(norm, I_M.col2)}
    return normalize(mul(norm, (float3x3)unity_WorldToObject));
#endif
}
```

`unity_WorldToObject` 是模型空间到世界空间的变换矩阵的逆矩阵，将法线与该矩阵进行左乘，就相当于进行了法线与该矩阵转置的右乘（左乘和右乘其实就是行和列的变换，所以左乘一个矩阵相当于右乘该矩阵的转置）。

接下来，我们需要得到世界空间下的光照方向，Unity 提供了`_WorldSpaceLightPos0` 这一参数供我们直接获取。同样的，获取光照颜色可以使用 `_LightColor0` 参数，不过需要引入 `Lighting.cginc`。不过要注意的是，此次演示的场景中只有一个光源且该光源为平行光，所以可以直接使用该参数。如果场景中存在多个光源且类型为其他种类，那么就不能够直接进行使用。

逐像素漫反射的代码与逐顶点类似，只不过计算的过程放到了片元着色器中：

```
v2f vert (appdata_base v)
{
    v2f o;
    o.pos = UnityObjectToClipPos(v.vertex);
    o.worldNormal = UnityObjectToWorldNormal(v.normal);

    return o;
}

fixed4 frag (v2f i) : SV_Target
{
    fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
    fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
    fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));
    fixed3 color = ambient + diffuse;

    return fixed4(color, 1.0);
}
```

代码写完之后，我们可以新建两个胶囊体，然后把材质分别挂到这两个物体上。观察物体的向光面与背光面的交界处，你会明显地发现逐顶点光照会使得交界处呈锯齿状，而逐像素光照则表现得更为平滑。但是，即便我们使用了逐像素光照，在光照无法到的区域内，模型的外观通常是全黑色的，整体的效果看上去十分怪异。为了让背光面有明暗的变化，我们可以使用**半兰伯特光照模型**。

原兰伯特模型使用了 max 操作让点积结果不为负值，但这样一来模型背光面的点积结果就会被强制限制在 0，使得背光面看上去是全黑色的。半兰伯特模型并没有使用 max，而是对其结果进行了一个 $\alpha$ 倍缩放和一个 $\beta$ 大小的偏移。一般来说这两个值都是 0.5，即公式为：

$$ c_{diffuse}=(c_{light} \cdot m_{diffuse})(0.5(n \cdot I) + 0.5) $$

这样一来，点积结果就会从 [-1, 1] 映射到 [0, 1] 内，而背光面的点积结果也会有所不同，不再是全部映射到 0 上。

代码的修改也是很简单的，我们可以在逐像素光照的基础上修改：

```
fixed4 frag (v2f i) : SV_Target
{
    fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
    fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
    fixed3 halfLambert = dot(i.worldNormal, worldLightDir) * 0.5 + 0.5;
    fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * halfLambert;
    fixed3 color = ambient + diffuse;

    return fixed4(color, 1.0);
}
```

下面是三种 Shader 的效果对比：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/02.png)

### 高光反射

高光反射需要用法线方向和光源方向计算出反射方向，具体的方法我在线性代数一章中有讲过，这里就不再赘述了。同样的，CG 中也提供了计算反射方向的函数 `reflect`。

逐顶点的高光反射如下：

```
Shader "Custom/Level3/SpecularVertex"
{
	Properties
	{
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)
		_Specular ("Specular", Color) = (1, 1, 1, 1)	// 材质的高光反射颜色
		_Gloss ("Gloss", Range(8.0, 256)) = 20			// 高光区域大小
	}
	SubShader
	{
		Tags { "RenderType"="Opaque" }
		LOD 100

		Pass
		{
			Tags { "LightMode"="ForwardBase" }

			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"
			#include "Lighting.cginc"

			fixed4 _Diffuse;
			fixed4 _Specular;
			float _Gloss;

			struct v2f
			{
				float4 pos : SV_POSITION;
				fixed3 color : COLOR;
			};
			
			v2f vert (appdata_base v)
			{
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);

				// 环境光
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

				// 将法线变换至世界坐标
				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
				// 将光照方向变换至世界坐标
				fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);

				// 计算反射光颜色
				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(worldNormal, worldLightDir));

				// 计算反射光方向
				fixed3 reflectDir = normalize(reflect(-worldLightDir, worldNormal));
				// 计算视角方向
				fixed3 viewDir = normalize(_WorldSpaceCameraPos.xyz - mul(unity_ObjectToWorld, v.vertex).xyz);

				// 计算高光反射颜色
				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(reflectDir, viewDir)), _Gloss);

				o.color = ambient + diffuse + specular;

				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				return fixed4(i.color, 1.0);
			}
			ENDCG
		}
	}

	Fallback "Specular"
}
```

由于 reflect 函数要求的入射方向是由光源指向交点处的，因此需要对 `worldLightDir` 取反后再传给 reflect 函数。之后再通过 `_WorldSpaceCameraPos` 获得世界空间中的摄像机位置，然后把顶点位置与之相减即可得到世界空间下的视角方向。

接下来就是逐像素的高光反射，只需要把计算逻辑放到片元着色器中即可：

```
v2f vert (appdata_base v)
{
    v2f o;
    o.pos = UnityObjectToClipPos(v.vertex);
    o.worldNormal = UnityObjectToWorldNormal(v.normal);
    o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

    return o;
}

fixed4 frag (v2f i) : SV_Target
{
    fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
    fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);

    fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

    fixed3 reflectDir = normalize(reflect(-worldLightDir, i.worldNormal));
    fixed3 viewDir = normalize(_WorldSpaceCameraPos.xyz - i.worldPos.xyz);

    fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(reflectDir, viewDir)), _Gloss);

    return fixed4(ambient + diffuse + specular, 1.0);
}
```

我在之前还介绍了另一种经验模型——Blinn 光照模型，它并没有计算反射方向，而是计算出了一个新的向量用于替代。代码实现如下：

```
fixed4 frag (v2f i) : SV_Target
{
    fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
    fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);

    fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * saturate(dot(i.worldNormal, worldLightDir));

    fixed3 viewDir = normalize(_WorldSpaceCameraPos.xyz - i.worldPos.xyz);
    fixed3 halfDir = normalize(worldLightDir + viewDir);

    fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(saturate(dot(i.worldNormal, halfDir)), _Gloss);

    return fixed4(ambient + diffuse + specular, 1.0);
}
```

新的向量 $h$ 是由视角方向和光照方向相加然后归一化得到的，这种计算方式比起原本的 Phong 模型要快一些，我们在绝大多数情况下都会选择 Blinn 模型。

下面是效果展示：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E5%85%89%E7%85%A7%E6%A8%A1%E5%9E%8B/03.png)

使用逐顶点的高光反射会让亮点边缘产生锯齿，而逐像素光照则要平滑得多。另外，Blinn 模型下的高光反射部分要看起来更大、更亮一些。