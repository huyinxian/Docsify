# 基础纹理

本篇笔记将对纹理这一概念作基本介绍，理清楚纹理在游戏开发中所起到的作用。

## 纹理

---

一般来说，我们会给物体覆盖上一张纹理贴图来代替物体的漫反射颜色。具体的做法其实很简单，我们只需要在之前的基础光照代码中加上纹理的属性即可：

```
Shader "Unlit/SimpleShader"
{
	Properties
	{
		_MainTex ("Main Tex", 2D) = "white" {}
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_Specular ("Specular", Color) = (1, 1, 1, 1)	// 材质的高光反射颜色
		_Gloss ("Gloss", Range(8.0, 256)) = 20			// 高光区域大小
	}
	SubShader
	{
		Tags { "RenderType"="Opaque" }
		LOD 100

		Pass
		{
			Tags { "LightMode"="ForwardBase" }

			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"
			#include "Lighting.cginc"

			sampler2D _MainTex;
			fixed4 _MainTex_ST;		// 用 纹理名_ST 命名即可获取纹理属性
			fixed4 _Color;
			fixed4 _Specular;
			float _Gloss;

			struct a2v
			{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 texcoord : TEXCOORD0;
			};

			struct v2f
			{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				float2 uv : TEXCOORD2;
			};
			
			v2f vert (appdata_base v)
			{
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				o.worldNormal = UnityObjectToWorldNormal(v.normal);
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				// 存储uv坐标，计算时先对纹理坐标缩放，在加上偏移量
				// 可以用宏代替：
				// o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uv = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;

				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);

				// 纹理采样
				fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;

				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;

				// 半兰伯特，背面不会显得太黑
				fixed3 diffuse = _LightColor0.rgb * albedo.rgb * (max(0, dot(worldNormal, worldLightDir)) * 0.5 + 0.5);

				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
				fixed3 halfDir = normalize(worldLightDir + viewDir);

				fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(max(0, dot(worldNormal, halfDir)), _Gloss);

				return fixed4(ambient + diffuse + specular, 1.0);
			}
			ENDCG
		}
	}

	Fallback "Specular"
}
```

在上述代码中，我们首先声明了一个纹理属性，然后在 CG 代码中定义 `_MainTex` 和 `_MainTex_ST` 两个变量。`_MainTex_ST` 相当于是纹理的偏移量和缩放量，我们只需要在 CG 代码中定义类似于 `纹理名_ST` 的变量即可拿到纹理属性（这是 Unity 帮我们自动完成的）。接下来，我们在顶点着色器传给片元着色器的结构体中新增了 `uv` 字段，这样一来片元着色器就可以获取对应的纹理坐标并加以运算。

效果大致如下：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/%E7%AE%80%E5%8D%95%E7%BA%B9%E7%90%86.png)

?> 纹理图片的面板中其实有很多可以设置的属性，这里我就不作展开了，有兴趣的可以查看一下 Unity 的官方文档。

## 凹凸映射

---

### 什么是凹凸映射

我们在绘制物体时，经常会遇到需要绘制表面不规则物体的情况，比如下面这种：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/normal_mapping_compare.png)

如果事先告诉你，你是否会认为右边的砖墙用的是另外的模型呢？当然这么想其实也没错，如果我们绘制一个高面数的模型，确实是能够达到预期的效果。然而作为游戏开发者，我们得时刻为游戏的性能表现和内存占用率着想，所以这种办法只能够暂时放弃掉了。

为了得出一个简单高效的解决方案，我们需要换一种角度，从光照的视角来看待这个问题：是什么决定了物体表面具有凹凸感？答案其实很简单，表面的法线向量决定了物体的形状。所以，我们并不需要真的去改变物体的顶点，仅仅只需要修改物体表面的法线向量，让光照相信该物体是凹凸不平的即可。

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/normal_mapping_surfaces.png)

这样一来，我们就只需要改变法线向量，仅用少量的开销便可以体现出表面的凹凸感。这种技术叫做**法线贴图**（Normal Mapping）。

### 法线贴图

为了让每个片元都能够使用各自的法线，我们需要提供一张法线贴图。然而，由于法线只表示方向，而方向又是相对于坐标空间而言的，我们该选择哪一种坐标空间来存储法线呢？

对于顶点所属的法线向量来说，它们是位于模型空间中的，所以第一种做法是把模型空间的法线修改过后直接存储在纹理上。不过在实际制作时，我们一般会采用另外一种坐标，即模型顶点的切线空间来存储法线。

由于法线是各个方向上都有的，因此它的取值范围在 `[-1, 1]`。如果要把法线信息存到一张 RGB 的贴图上，那么我们就需要将法线进行一些小小的变换：

$$ rgb = \frac{normal + 1}{2} $$

这样一来，法线信息就被存储到了范围为 `[0, 1]` 的贴图上。如果我们采用的是切线空间，那么法线贴图大概长这个样子：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/normal_mapping_normal_map.png)

切线空间的法线贴图基本都是蓝色的，而模型空间的法线贴图则是五颜六色的。对于一个模型而言，每个顶点的切线空间都是不同的，但其位于切线空间下的法线向量大部分都是 $(0, 0, 1)$，也就是朝向 z 轴的。将这个向量用上面的公式进行转换后，得到的 RGB 颜色是 $(0.5, 0.5, 1)$（浅蓝色）。相比之下，由于模型空间下的法线朝向各个方向，所以得到的颜色就是五颜六色的。

那么为什么一定要多弄一个切线空间的法线贴图呢？为什么不直接把模型空间的法线存储到贴图中呢？其实用什么坐标空间都可以，我们最终的目的是要拿这些数据来计算光照的。现在假设我们用的法线贴图是处于模型空间下的，并且贴图中的法线指向 z 方向，那么当我们把这张贴图用于表面法线指向 y 方向的平面，那么得到的效果就会完全不一样：

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/normal_mapping_ground_normals.png)

可以看到，如果平面的表面法线和法线贴图的法线方向不一致，那么光照的效果和光照位置就完全对不上了。这主要是因为法线贴图中的法线向量处于模型空间下，它们都是指向 z 方向的，而按照我们的预期效果，这些法线应该是跟随表面法线指向 y 方向才对。对于一个复杂的模型而言，它的法线贴图是对应的是制作模型时的那一刻，使用贴图时必须要和模型的初始朝向对应上。如果模型进行了运动，那么还得记录下模型的变换，这是非常麻烦的。

另一种最常用的解决方案就是使用切线空间。切线空间是位于三角形表面上的空间，它可以被认为是法线向量的本地空间，并且法线在这个空间内是永远指向正 z 方向的。

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/normal_mapping_tbn_vectors.png)

法线、切线（Tangent）、副切线（Bitangent）相互垂直，构成了一个切线空间，并且切线和副切线是沿着纹理坐标的两个方向对齐的。利用这个特性，我们可以得到一个计算切线和副切线的公式。

![](http://cdn.fantasticmiao.cn/image/post/Unity/Advanced/%E5%9F%BA%E7%A1%80%E7%BA%B9%E7%90%86/normal_mapping_surface_edges.png)

在上图中，三角面 $P_1P_2P_3$ 的三个顶点坐标已知，我们的目的就是要通过它们来求出切线和副切线。现在，我们让边 $E_2$ 和纹理坐标差 $\Delta U_2、\Delta V_2$ 组成一个三角形（$E_1$ 和 $\Delta U_1、\Delta V_1$ 同理），则可以得到如下等式：

$$ E_1 = \Delta U_1 T + \Delta V_1 B $$
$$ E_2 = \Delta U_2 T + \Delta V_2 B $$

也就是这样：

$$ (E_{1x},E_{1y},E_{1z}) = \Delta U_1(T_x,T_y,T_z) + \Delta V_1(B_x,B_y,B_z) $$
$$ (E_{2x},E_{2y},E_{2z}) = \Delta U_2(T_x,T_y,T_z) + \Delta V_2(B_x,B_y,B_z) $$

我们可以把它们写成矩阵乘法的形式：

$$
\begin{bmatrix}
   E_{1x} & E_{1y} & E_{1z} \\
   E_{2x} & E_{2y} & E_{2z}
\end{bmatrix} =
\begin{bmatrix}
   \Delta U_1 & \Delta V_1 \\
   \Delta U_2 & \Delta V_2
\end{bmatrix}
\begin{bmatrix}
   T_x & T_y & T_z \\
   B_x & B_y & B_z
\end{bmatrix}
$$

我们让等式两边乘以 $\Delta U\Delta V$ 的逆矩阵：

$$
\begin{bmatrix}
   \Delta U_1 & \Delta V_1 \\
   \Delta U_2 & \Delta V_2
\end{bmatrix}^{-1}
\begin{bmatrix}
   E_{1x} & E_{1y} & E_{1z} \\
   E_{2x} & E_{2y} & E_{2z}
\end{bmatrix} =
\begin{bmatrix}
   T_x & T_y & T_z \\
   B_x & B_y & B_z
\end{bmatrix}
$$

计算逆矩阵的过程就不多赘述了，大致就是用它的伴随矩阵除以它的行列式，如果不理解的话可以往前翻一翻线性代数基础一章。

$$
\begin{bmatrix}
   T_x & T_y & T_z \\
   B_x & B_y & B_z
\end{bmatrix} =
\frac{1}{\Delta U_1\Delta V_2 - \Delta U_2\Delta V_1}
\begin{bmatrix}
   \Delta V_2 & -\Delta V_1 \\
   -\Delta U_2 & \Delta U_1
\end{bmatrix}
\begin{bmatrix}
   E_{1x} & E_{1y} & E_{1z} \\
   E_{2x} & E_{2y} & E_{2z}
\end{bmatrix}
$$

这样一来，我们就可以用三角面的顶点及其纹理坐标求出切线和副切线，进而得到与之垂直的法线向量。

总而言之，使用模型空间的法线贴图有如下好处：

* 实现简单且够直观，计算时也不需要模型的原始法线和切线。
* 在边界处的缝隙较少，可以提供平滑的边界效果。这主要是因为模型空间下的法线贴图用的是同一坐标系，可以在边界处进行插值。

切线空间的法线贴图有如下优点：

* 自由度高。由于贴图用的是相对的法线信息，因此用于其他模型上时也能得到相对合理的效果。
* 可以进行 UV 动画，即可以移动纹理的 UV 坐标实现凹凸移动的效果，多用于水面等效果。如果采用的是模型空间坐标则会得到完全错误的效果。
* 可压缩。由于切线空间下的法线永远指向正 z 方向，因此可以仅存储 xy 坐标，然后推导出 z 方向。